-- Rules for the 'Roller' parser
-- Written in Labelled BNF grammar as used by bnfc
-- mrZalli

ExpCmd.		Cmd ::= Exp;
StmtCmd.	Cmd ::= Stmt;

token VarIdent	(letter | '_')+;
--token VarIdent	( ('a'-'c') | ('e'-'z') | upper ) ( letter | '_' )*;

-- These are parsed in the interactive parser by C/C++ libraries
token IntLiteral '-'? digit+;
token FloatLiteral '-'? digit+ '.' digit+ ('e' '-'? digit+)?;

comment "//";
comment "/*" "*/";


-- Expressions --

coercions Exp 3;

EAdd. 	Exp1 	::= Exp1 "+" Exp2;
ESub. 	Exp1 	::= Exp1 "-" Exp2;
EMul. 	Exp2 	::= Exp2 "*" Exp3;
EDiv. 	Exp2 	::= Exp2 "/" Exp3;

separator Exp ",";


-- -- Values -- --

-- An integer numeral value.
NumInt.		Numeral	::= IntLiteral;
NumFloat.	Numeral ::= FloatLiteral;

-- An integer literal, string or variable value
EVal. 	Exp3	::= Val;

ValNum.	Val	::= Numeral;
ValVar.	Val	::= VarIdent;
ValStr.	Val	::= String;


-- -- Sequences -- --

EList.		Exp3	::= "{" [Exp] "}";


-- -- Ranges -- --

ERange.		Exp3		::= ExpRange;

-- Ranges use numeral values and a linear step is assumed with the default of 1
-- These can be treated as objects or sequences, but trying to evaluate the sequence to the infinite end of an
-- infinite range should be a terminating error.

Range.		ExpRange	::= Exp ".." Exp;
RStep.		ExpRange	::= Exp "," Exp2 ".." Exp3;

RInf.		ExpRange	::= Exp "..";
RStepInf.	ExpRange	::= Exp "," Exp2 "..";
RNInf.		ExpRange	::= ".." Exp;
RStepNInf.	ExpRange	::= ".." Exp "," Exp2;


-- -- Dice expressions -- --

EDice.	Exp3	::= ExpD;

E1d6.	ExpD	::= "d";
E1dN.	ExpD	::= "d" Exp;
ENd6.	ExpD	::= Exp "d";
ENdN.	ExpD	::= Exp "d" Exp;


-- -- Keyword functions -- --

EKeyW.		Exp	::= ExpKW;

-- Gives the length of the sequence. Gives 1 for scalars
EKWCount.	ExpKW	::= "Count" Exp;

-- Gives the sum of the sequence. default operation when converting from a sequence to a single value
EKWSum.		ExpKW	::= "Sum" Exp;

EKWRepeat.	ExpKW	::= "Repeat" Exp Exp;
EKWMean.	ExpKW	::= "Mean" Exp; 

-- Performs accumulation/folding operation.
-- Also maps if there is only one parameter for the function
EKWAcc.		ExpKW	::= "Acc" Exp VarIdent;


-- -- List operations -- --

coercions Pred 3;


PredBranch.	Pred1	::= Pred1 "," Pred2;
PredAnd.	Pred1	::= Pred1 "&" Pred2;
PredOr.		Pred1	::= Pred1 "|" Pred2;
PredXOr.	Pred1	::= Pred1 "^" Pred2;

PredEQ.		Pred2	::= "=" Val;
--PredNEQ.	Pred2	::= "!=" Val;
PredGT.		Pred2	::= "<" Val;
PredLT.		Pred2	::= ">" Val;
PredGTEq.	Pred2	::= "<=" Val;
PredLTEq.	Pred2	::= ">=" Val;

-- Negates the predicate.
PredNot.	Pred3	::= "!" Pred;

PredIsStr.	Pred3	::= "$";
PredIsInt.	Pred3	::= "#";
PredIsFloat.Pred3	::= "%";

PredInd.	Pred3	::= Val; -- Indexes
PredRange.	Pred3	::= ExpRange; -- Slices

ESeqFilt.	Exp3	::= Exp "[" Pred "]"; -- Predicate expression, filters or indexes the vector


-- Function call --

ECall.	Exp3	::= VarIdent "(" [Exp] ")";


-- Statements --

SVarAs.		Stmt	::= VarIdent "=" Exp;
SFunDef.	Stmt	::= VarIdent "(" [Exp] ")" "=" Exp;
